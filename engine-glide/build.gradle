apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
if (MAVEN_LOCAL.toBoolean()) {
  apply plugin: 'maven'
} else {
  apply plugin: 'com.novoda.bintray-release'
}
apply from: '../gradle/klint.gradle'

android {
  resourcePrefix "pejoy"

  compileSdkVersion versions.build.target_sdk
  buildToolsVersion versions.build.build_tools

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion versions.build.target_sdk
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  compileOptions {
    kotlinOptions.freeCompilerArgs += ['-module-name', "io.github.keep2iron.pejoy.pejoy_glide"]
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  api project(':core')
  compileOnly "io.github.keep2iron:pineapple-glide:0.0.2-alpha02"
  compileOnly 'com.github.bumptech.glide:glide:4.10.0'
  implementation deps.support.app_compat
  implementation deps.kotlin.stdlib_jdk7
  implementation 'com.github.chrisbanes:PhotoView:2.3.0'

  testImplementation deps.test.junit
  androidTestImplementation deps.test.runner
  androidTestImplementation deps.test.rules
  androidTestImplementation(deps.test.espresso_core) {
    exclude group: 'com.android.support'
    exclude module: 'appcompat-v7'
    exclude module: 'support-v4'
  }
  androidTestImplementation(deps.test.espresso_contrib) {
    exclude group: 'com.android.support'
    exclude module: 'appcompat-v7'
    exclude module: 'support-v4'
  }
}

if (MAVEN_LOCAL.toBoolean()) {
  group = rootProject.ext.version_group
  version = rootProject.ext.version_pejoy_engin_glide

  uploadArchives {
    repositories {
      mavenDeployer {
        // 唯一标识
        pom.groupId = rootProject.ext.version_group
        // 项目名称
        pom.artifactId = "pejoy-glide"
        repository(url: uri(project.MAVEN_LOCAL_REPO))
        // 版本号
        pom.version = rootProject.ext.version_pejoy_engin_glide
      }
    }
  }

  // 指定编码
  tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
  }

  // 打包源码
  task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
  }

  task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
  }

  // 制作文档(Javadoc)
  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  artifacts {
    archives sourcesJar
    archives javadocJar
  }
} else {
  publish {
    userOrg = 'keep2iron'
    groupId = rootProject.ext.version_group
    artifactId = 'pejoy-glide'
    publishVersion = rootProject.ext.version_pejoy_engin_glide
    desc = 'image picker,base on matisse'
    website = 'https://keep2iron.github.io'
  }
}